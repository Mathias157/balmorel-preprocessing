# Load the config file at the top
configfile: "assumptions.yaml"

out_path = "Output/"
data_path = "Data/"
modules_path = "Modules/"
submod_path = "Modules/Submodules/"
weather_year=config['timeseries']['weather_year']

# 1. General Purpose
rule all:
    input:
        [
            f'{out_path}DE.inc', 
            f'{out_path}FLEXDEM_FLEXYDEMAND.inc',
            f"{out_path}DH.inc",
            f"{out_path}DH_VAR_T.inc",
            f"{out_path}CCC.inc",
            f"{out_path}XINVCOST.inc",
            f"{out_path}AGKN.inc",
            f"{out_path}HYDROGEN_SYNFUELDEMAND.inc",
            f"{out_path}WND_VAR_T.inc",
            f'{out_path}OFFSHORE_WND_VAR_T.inc',
            f"{out_path}GKFX.inc",
            f"{out_path}GMAXF.inc",
            f"{out_path}FUELTRANSPORT_COST.inc",
            f"{out_path}SUBTECHGROUPKPOT2.inc"
        ]

rule create_conversion_dictionaries:
    output:
        [f"{submod_path}exo_elec_dem_conversion_dictionaries.pkl",
        f"{submod_path}exo_heat_dem_conversion_dictionaries.pkl",
        f"{submod_path}exo_grid_conversion_dictionaries.pkl"]
    script:
        f"{submod_path}create_conversion_dictionaries.py"

# 2. Exogenous Electricity Demands
rule format_energinet_data:
    input:
        f"{data_path}Timeseries/ElConsumptionEnerginet2023.csv"
    output:
        f"{data_path}Timeseries/energinet_eldem.nc"
    shell:
        """
        python {modules_path}format_energinet.py --energinet-data-path={input}
        """

rule exo_electricity_demand:
    input:
        [f"{submod_path}exo_elec_dem_conversion_dictionaries.pkl", 
        f"{data_path}Timeseries/energinet_eldem.nc",
        f"{modules_path}exo_electricity_demand.py"]
    output:
        [f"{out_path}DE.inc", f"{out_path}DE_VAR_T.inc"]
    shell:
        """
        python {input[2]} --conversion-file={input[0]} --el-dataset={input[1]} --show-difference=False
        """

# 3. Exogenous Heat Demands
rule format_vpdk21_data:
    input:
        expand(f"{data_path}AAU Kommuneplan")
    output:
        [f"{data_path}AAU Kommuneplan/districtheat_exo_heatdem.nc", 
        f"{data_path}AAU Kommuneplan/industry_exo_heatdem.nc"]
    script:
        f"{modules_path}format_vpdk21.py"

rule format_dkstat_industry_data:
    input:
        [f"{data_path}Danmarks Statistik/Industriforbrug Type.xlsx",
        f"{modules_path}format_dkstat.py"]
    output:
        f"{data_path}Danmarks Statistik/industry_demand.nc"
    shell:
        """
        python {input[1]} --get-industry-demand
        """


rule heat_profiles:
    input:
        [
            f"{modules_path}heat_profiles.py",
            f"M:/Project/RQII/DK+Surrounding2012.nc"
        ]
    params:
        weather_year=config["timeseries"]["weather_year"]
    output:
        [
            f"{out_path}DH_VAR_T.inc",
            f"{out_path}INDIVUSERS_DH_VAR_T.inc"
        ]
    shell:
        """
        python {modules_path}heat_profiles.py generate {input[1]} --weather-year={params.weather_year} --plot
        """

rule exo_heat_demand:
    input:
        [
            f"{submod_path}exo_heat_dem_conversion_dictionaries.pkl",
            f"{data_path}AAU Kommuneplan/districtheat_exo_heatdem.nc", 
            f"{data_path}AAU Kommuneplan/industry_exo_heatdem.nc",
            f"{data_path}Danmarks Statistik/industry_demand.nc",
            f"{data_path}Timeseries/energinet_eldem.nc",
        ]
    output:
        [f"{submod_path}districtheat_sets.pkl",
        f"{submod_path}individual_sets.pkl",
        f"{submod_path}ind-lt_sets.pkl",
        f"{submod_path}ind-mt_sets.pkl",
        f"{submod_path}ind-ht_sets.pkl",
        f'{out_path}DH.inc',
        f'{out_path}INDUSTRY_DH.inc', 
        f'{out_path}INDUSTRY_DH_VAR_T.inc',
        f'{out_path}INDIVUSERS_DH.inc']
    script:
        f"{modules_path}exo_heat_demand.py"

# 4. Transport Demand
rule format_dkstat_transport_data:
    input:
        [f"{data_path}Danmarks Statistik/Transportforbrug Type.xlsx",
        f"{modules_path}format_dkstat.py"]
    output:
        f"{data_path}Danmarks Statistik/transport_demand.csv"
    shell:
        """
        python {input[1]} --get-transport-demand --include-bunkering=false
        """

rule transport_road_demand:
    input: 
        [
            f'{data_path}Danmarks Statistik/transport_demand.csv',
            f'{data_path}Gas, Transport and Industry Data/gdf_all_ETISplus.geojson',
            f"{modules_path}transport_road_demand.py"
        ]
    params:
        charging_capacity_per_vehicle=config['grid_assumptions']['electricity']['charging_capacity_per_vehicle']
    output:
        [f'{out_path}FLEXDEM_FLEXYDEMAND.inc',
        f'{out_path}FLEXDEM_FLEXMAXLIMIT.inc']
    shell: 
        """
        python {modules_path}transport_road_demand.py --chargercap={params.charging_capacity_per_vehicle}
        """

rule transport_heavy_demand:
    input:
        [
            f"{data_path}Danmarks Statistik/transport_demand.csv"
        ]
    params:
        meoh_per_jetfuel=config["fuel_assumptions"]["meoh_per_jetfuel"],
        jetfuel_demand=config["fuel_assumptions"]["jetfuel_demand"],
        shipping_demand=config["fuel_assumptions"]["shipping_demand"]
    output:
        f"{out_path}HYDROGEN_SYNFUELDEMAND.inc" 
    shell:
        """
        python {modules_path}transport_heavy_demand.py --meoh-per-jetfuel={params.meoh_per_jetfuel} --shipping-demand={params.shipping_demand} --jetfuel-demand={params.jetfuel_demand}
        """

# 5. Sets
rule geographic_sets:
    input:
        [
            f"{submod_path}districtheat_sets.pkl",
            f"{submod_path}individual_sets.pkl",
            f"{submod_path}ind-lt_sets.pkl",
            f"{submod_path}ind-mt_sets.pkl",
            f"{submod_path}ind-ht_sets.pkl",
        ]
    output:
        f"{out_path}CCC.inc"
    script:
        f"{modules_path}geographic_sets.py"

rule investment_options:
    input:
        [
            f"{modules_path}investment_options.py",
            f"{submod_path}districtheat_sets.pkl",
            f"{submod_path}individual_sets.pkl",
            f"{submod_path}ind-lt_sets.pkl",
            f"{submod_path}ind-mt_sets.pkl",
            f"{submod_path}ind-ht_sets.pkl", 
            f"{data_path}Timeseries/energinet_eldem.nc",
            f"{data_path}AAU Kommuneplan/districtheat_exo_heatdem.nc", 
            f"{data_path}AAU Kommuneplan/industry_exo_heatdem.nc",
        ]
    params:
        large_munis=config['zones']['large_munis'],
        medium_munis=config['zones']['medium_munis']
    output:
        [
            f"{out_path}AGKN.inc",
            f"{out_path}HYDROGEN_AGKN.inc",
            f"{out_path}INDUSTRY_AGKN.inc",
        ]
    shell:
        """
        python {modules_path}investment_options.py --large-munis="{params.large_munis}" --medium-munis="{params.medium_munis}"
        """

# 6. Grids
rule grids:
    input:
        [
            f"{data_path}BalmorelData/municipal_connectivity.nc",
            f"{submod_path}exo_grid_conversion_dictionaries.pkl",
            f"assumptions.yaml",
            f"{modules_path}grids.py"
        ]
    output:
        [
            f"{out_path}XINVCOST.inc",
            f"{out_path}XLOSS.inc",
            f"{out_path}DISCOST_E.inc",
            f"{out_path}DISLOSS_E.inc",
            f"{out_path}DISLOSS_E_AG.inc",
            f"{out_path}INDUSTRY_DISLOSS_E_AG.inc",
            f"{out_path}INDIVUSERS_DISLOSS_E_AG.inc",
            f"{out_path}HYDROGEN_XH2INVCOST.inc",
            f"{out_path}HYDROGEN_XH2COST.inc",
            f"{out_path}HYDROGEN_XH2LOSS.inc"
        ]
    shell:
        """
        python {modules_path}grids.py
        """
        
rule biomass_transport:
    input: 
        f"{data_path}BalmorelData/municipal_connectivity.nc"
    output:
        f"{out_path}FUELTRANSPORT_COST.inc"
    params:
        transport_cost=config['resources']['biotransportcost']
    shell:
        """
        python {modules_path}biomass_transport.py transport {params.transport_cost}
        """

# 7. Other
rule vre_profiles:
    input:
        [
            f"{modules_path}vre_profiles.py",
        ]
    params:
        cutout_path=config["resources"]["cutout_path"],
        weather_year=config["timeseries"]["weather_year"]
    output:
        [
            f"{out_path}WND_VAR_T.inc",
            f"{out_path}SOLE_VAR_T.inc",
            f"{out_path}WNDFLH.inc",
            f"{out_path}SOLEFLH.inc",
            f'Output/VRE/{weather_year}_offshore_wind.nc'
        ]
    shell:
        """
        python {modules_path}vre_profiles.py --cutout-path={params.cutout_path} --weather-year={params.weather_year} && \
        python {modules_path}vre_profiles.py --cutout-path={params.cutout_path} --weather-year={params.weather_year} --offshore-profiles=True
        """

rule offshore_wind:
    input:
        [
            f"{modules_path}offshore_wind.py",
            f'Output/VRE/{weather_year}_offshore_wind.nc'
        ]
    params:
        total_offshore_wind_potential=config['resources']['total_offshore_wind_potential'],
        weather_year=config["timeseries"]["weather_year"]
    output:
        f'{out_path}OFFSHORE_WND_VAR_T.inc'
    shell:
        """
        python {modules_path}offshore_wind.py --weather-year={weather_year} --total-offshore-wind-potential={params.total_offshore_wind_potential} 
        """

rule exo_powerplants:
    input:
        [
            f'{data_path}Powerplants (Energistyrelsen)/ept2020_anlaeg_stamdata_m_hovedbraensel_og_braendselsfordeling_geoxy_0.xlsx',
            f'{data_path}Powerplants (Energistyrelsen)/anlaeg.xlsx'
        ]
    output:
        f"{out_path}GKFX.inc"
    script:
        f"{modules_path}exo_powerplants.py"

rule biomass_availability:
    input:
        [
            f"{modules_path}format_balmorel_data.py",
            f"{data_path}BalmorelData"
        ]
    params:
        model_path=config["balmorel_input"]["model_path"],
        scenario=config["balmorel_input"]["scenario"],
        load_again=config["balmorel_input"]["load_again"],
        woodpot=config['resources']['woodpot'],
        strawpot=config['resources']['strawpot'],
        biogaspot=config['resources']['biogaspot'],
        woodimport=config['resources']['woodimport']
    output:
        [
            f"{out_path}GMAXF.inc"
        ]
    shell:
        """
        python {input[0]} --model-path={params.model_path} --scenario={params.scenario} --load-again={params.load_again} biomass-availability --woodpot={params.woodpot} --strawpot={params.strawpot} --biogaspot={params.biogaspot} --woodimport={params.woodimport}
        """

rule technology_potentials:
    params:
        land_for_PTES=config['resources']['available_land_for_PTES']
    output:
        f"{out_path}SUBTECHGROUPKPOT2.inc"
    shell:
        """
        python {modules_path}tech_potentials.py ptes {params.land_for_PTES}
        """