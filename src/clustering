# Load the config file at the top
configfile: "clustering.yaml"

out_path     = "ClusterOutput/"
data_path    = "Data/"
modules_path = "Modules/"
submod_path  = "Modules/Submodules/"
balmorel_path = config['balmorel_input']['model_path']
scenario = config['balmorel_input']['scenario']
balmorel_sc_folder = f"{balmorel_path}/{scenario}/model/"

# 1. General Purpose
rule all:
    input:
        [   
            f"{out_path}DE.inc",
            f"{out_path}HYDROGEN_AGKN.inc",
        ]

rule collect_balmorel_input:
    output:
        f"{balmorel_sc_folder}{scenario}_input_data.gdx"
    run:
        from pybalmorel import Balmorel
        model = Balmorel(balmorel_path)
        model.load_incfiles(scenario)

rule cluster:
    input:
        [
            f"{balmorel_sc_folder}{scenario}_input_data.gdx",
            f"{modules_path}clustering.py"
        ]
    params:
        cluster_params=config['clustering']['data_for_clustering'],
        aggregation_functions=config['clustering']['aggregation_functions'],
        cluster_size=config['clustering']['cluster_size']
    output:
        [
            f"{out_path}clustering.gpkg",
            f"{out_path}{'-'.join(config['clustering']['data_for_clustering'].replace(' ', '').split(','))}_{config['clustering']['cluster_size']}cluster_geofile.gpkg"
        ]
    shell:
        """
        python {modules_path}clustering.py --model-path={balmorel_path} --scenario={scenario} --cluster-params "{params.cluster_params}" --aggregation-functions="{params.aggregation_functions}" --cluster-size={params.cluster_size}
        """

rule aggregate_inputs:
    input:
        [
            f"{balmorel_sc_folder}{scenario}_input_data.gdx",
            f"{out_path}clustering.gpkg",
            f"{modules_path}aggregate_inputs.py"
        ]
    params:
        model_path = balmorel_path,
        scenario = scenario,
        exceptions = config['aggregation']['exceptions'],
        mean_aggfuncs = config['aggregation']['mean_aggfuncs'],
        median_aggfuncs = config['aggregation']['median_aggfuncs'],
        zero_fillnas = config['aggregation']['zero_fillnas'],
        cluster_params=config['clustering']['data_for_clustering'],
        cluster_size=config['clustering']['cluster_size']
    output:
        [
            f"{out_path}CCCRRRAAA.inc",
            f"{out_path}CCCRRR.inc",
            f"{out_path}RRRAAA.inc",
            f"{out_path}RRR.inc",
            f"{out_path}AAA.inc",
            f"{out_path}DE.inc",
            f"{out_path}DH.inc",
            f"{out_path}XINVCOST.inc",
            f"{out_path}XLOSS.inc",
            f"{out_path}XCOST.inc",
            f"{out_path}HYDROGEN_XH2INVCOST.inc",
            f"{out_path}HYDROGEN_XH2LOSS.inc",
            f"{out_path}HYDROGEN_XH2COST.inc",
            # ...and many more
        ]
    shell:
        """
        python {modules_path}aggregate_inputs.py --model-path={params.model_path} --scenario={params.scenario} --exceptions="{params.exceptions}" --mean-aggfuncs="{params.mean_aggfuncs}" --median-aggfuncs="{params.median_aggfuncs}" --zero-fillnas="{params.zero_fillnas}" --cluster-params "{params.cluster_params}" --cluster-size={params.cluster_size}
        """

rule create_addon_files:
    input:
        f"{out_path}clustering.gpkg"
    output:
        [
            f"{out_path}INDUSTRY_INDUSTRY_AAA.inc",
            f"{out_path}INDUSTRY_CCCRRRAAA.inc",
            f"{out_path}INDUSTRY_RRRAAA.inc",
            f"{out_path}INDUSTRY_AAA.inc",
            f"{out_path}INDUSTRY_AGKN.inc",
            f"{out_path}INDUSTRY_DH.inc",
            f"{out_path}INDUSTRY_DE.inc",
            f"{out_path}INDIVUSERS_INDIVUSERS_AAA.inc",
            f"{out_path}INDIVUSERS_CCCRRRAAA.inc",
            f"{out_path}INDIVUSERS_RRRAAA.inc",
            f"{out_path}INDIVUSERS_AAA.inc",
            f"{out_path}INDIVUSERS_AGKN.inc",
            f"{out_path}INDIVUSERS_DH.inc",
            f"{out_path}INDIVUSERS_DE.inc",
            f"{out_path}HYDROGEN_AGKN.inc",
        ]
    script:
        f"{modules_path}create_addon_files.py"